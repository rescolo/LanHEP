%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%Two Higgs Doublets Models-type III-higgs basis-t'Hooft-feynman gauge.%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

model 'thdm3(Higgs-basis)'/1.

option ReduceGamma5=0.
let g5=gamma5.
use th_tex.

prtcprop pdg.

%prtc#.mdl is one of the output files of LanHEP. To make
% it compatible with CalcHEP we need to change their format
% to include the PDG particle number as the third column

prtcformat fullname:'  Full Name  ',
           name:' P ',
           aname:' aP ',
           pdg:' number ',
           spin2, mass, width, color, aux,
           texname:'>  LaTeX P name  <',
           atexname:'>  LaTeX aP name < '.

%%%%%%%%%%%%%%%%%%%%%%%% SM parameters %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

parameter  EE  = 0.31333 : 'Electromagnetic coupling constant (<->1/128)',
	   GG  = 1.117   : 'Strong coupling constant (Z point)  (PDG-94)',
	   SW  = 0.4740  : 'Sin of the Weinberg angle (PDG-94,"on-shell")',
           s12 = 0.221   : 'Parameter of C-K-M matrix (PDG-94)',
	   s23 = 0.040   : 'Parameter of C-K-M matrix (PDG-94)',
           s13 = 0.0035  : 'Parameter of C-K-M matrix (PDG-94)'.

parameter  CW  = sqrt(1-SW**2) : 'Cos of the Weinberg angle'.

parameter  c12  = sqrt(1-s12**2) : 	'Parameter  of C-K-M matrix',
           c23  = sqrt(1-s23**2) : 	'Parameter  of C-K-M matrix',
           c13  = sqrt(1-s13**2) : 	'Parameter  of C-K-M matrix'.

parameter  Vud = c12*c13 		: 'C-K-M matrix element',
	   Vus = s12*c13 		: 'C-K-M matrix element',
	   Vub = s13     		: 'C-K-M matrix element',
           Vcd = (-s12*c23-c12*s23*s13) : 'C-K-M matrix element',
           Vcs = (c12*c23-s12*s23*s13)  : 'C-K-M matrix element',
	   Vcb = s23*c13 		: 'C-K-M matrix element',
	   Vtd = (s12*s23-c12*c23*s13) 	: 'C-K-M matrix element',
	   Vts = (-c12*s23-s12*c23*s13)	: 'C-K-M matrix element',
	   Vtb = c23*c13  		: 'C-K-M matrix element'.

OrthMatrix( { {Vud,Vus,Vub}, {Vcd,Vcs,Vcb}, {Vtd,Vts,Vtb}} ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% gauge particles %%%%%%%%%%%%%%%%%%%%%%%%%%%

vector  
	A/A: (photon, gauge),
	Z/Z:('Z boson', mass MZ = 91.187, width wZ = 2.502, gauge),
	G/G: (gluon, color c8, gauge),
	'W+'/'W-': ('W boson', mass MW = MZ*CW, width wW = 2.094, gauge).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% spinores %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

spinor 		n1:(neutrino,left), 	   e1:(electron, mass Me = 0.0005),
		n2:('mu-neutrino',left),   e2:(muon, mass Mm  = 0.1057),
		n3:('tau-neutrino',left),  e3:('tau-lepton', mass Mt  = 1.777).

spinor		u:('u-quark',color c3, mass Mup = 0.0017),
		d:('d-quark',color c3, mass Md = 0.0041),
		c:('c-quark',color c3, mass Mc  = 1.300),
		s:('s-quark',color c3, mass Ms = 0.200),
		t:('t-quark',color c3, mass Mtop = 170, width wtop = 1.442),
		b:('b-quark',color c3, mass Mb =  4.300 ).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% scalars %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

scalar h/h: ('light Higgs', mass Mh=160, width wh=1.461),
       H/H: ('heavy Higgs', mass MHH=210, width wHH=1.0),
       A0/A0: ('cp-odd Higgs', mass MA0=220, width wA0=1.0),
       'H+'/'H-': ('charged Higgs', mass MHc=205, width wHc=1.0).

%%%%%%%%%%%%%%%%%%%%%%%% fermions doblets %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
			
let l1={n1,e1}, L1={N1,E1}.
let l2={n2,e2}, L2={N2,E2}.
let l3={n3,e3}, L3={N3,E3}.

let q1={u,d}, Q1={U,D}, q1a={u,Vud*d+Vus*s+Vub*b}, Q1a={U,Vud*D+Vus*S+Vub*B}.
let q2={c,s}, Q2={C,S}, q2a={c,Vcd*d+Vcs*s+Vcb*b}, Q2a={C,Vcd*D+Vcs*S+Vcb*B}. 
let q3={t,b}, Q3={T,B}, q3a={t,Vtd*d+Vts*s+Vtb*b}, Q3a={T,Vtd*D+Vts*S+Vtb*B}.

%%%%%%%%%%%%%%%%%%%%%%%% electrodebil interaction %%%%%%%%%%%%%%%%%%%%%%%%%%%%%

let B1= -SW*Z+CW*A, W3=CW*Z+SW*A, W1=('W+'+'W-')/Sqrt2,
	 W2 = i*('W+'-'W-')/Sqrt2.

let gh1 = ('W+.c'+'W-.c')/Sqrt2, gh2= i*('W+.c'-'W-.c')/Sqrt2,
		gh3= CW*'Z.c'+SW*'A.c', gh={gh1,gh2,gh3}.

let Gh1 = ('W+.C'+'W-.C')/Sqrt2, Gh2=i*('W+.C'-'W-.C')/Sqrt2, 
		Gh3= CW*'Z.C'+SW*'A.C', Gh={Gh1,Gh2,Gh3}. 

let WW1 = {W1,  W2 , W3}, WW = {'W+',W3,'W-'}.

let g=EE/SW, g1=EE/CW.

%%%%%%%%%%%%%%%%% Self-interaction of gauge bosons %%%%%%%%%%%%%%%%%%%%%%%%

lterm -F**2/4   where 
	F=deriv^mu*B1^nu-deriv^nu*B1^mu.

lterm -F**2/4  where
	F=deriv^mu*G^nu^a-deriv^nu*G^mu^a+i*GG*f_SU3^a^b^c*G^mu^b*G^nu^c.

lterm -F**2/4  where
        F=deriv^mu*WW1^nu^a-deriv^nu*WW1^mu^a -g*eps^a^b^c*WW1^mu^b*WW1^nu^c.

%%%%%%%%%%%%%%%%%%% left fermion interaction with gauge fields %%%%%%%%%%%%%%%%%%

lterm  	anti(psi)*gamma*(1-g5)/2*(i*deriv-g*taupm*WW/2-Y*g1*B1)*psi
		where 
			psi=l1,  Y=-1/2;
			psi=l2,  Y=-1/2;
			psi=l3,  Y=-1/2;
			psi=q1a, Y= 1/6;
			psi=q2a, Y= 1/6;
			psi=q3a, Y= 1/6.

%%%%%%%%%%%%%%%%%%%% right fermion interaction with gauge fields %%%%%%%%%%%%%%%%%

lterm  	anti(psi)*gamma*(1+g5)/2*(i*deriv - Y*g1*B1)*psi
		where 
			psi=e1,Y= -1;
			psi=e2,Y= -1;
			psi=e3,Y= -1;
			psi=u, Y=  2/3;
			psi=c, Y=  2/3;
			psi=t, Y=  2/3;
			psi=d, Y= -1/3;
			psi=s, Y= -1/3;
			psi=b, Y= -1/3.

%%%%%%%%%%%%%%%%%%%%%%%%% quark-gluon interaction %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

lterm  GG*anti(psi)*lambda*gamma*G*psi where
	psi=q1; psi=q2; psi=q3.

%%%%%%%%%%%%%%%%%%%%%%%% model parameters %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

let  	   vvev = 2*MW*SW/EE.
parameter  md2 = 10000:'minimal condition',
	   la2 = 2.24484:'Lambda-2', la7 = -0.05374:'Lamda-7'.

parameter  sbma = 0.76:'Sin(beta-alpha)'.
parameter  cbma = sqrt(1-sbma**2):'Cos(beta-alpha)'.

%parameter  rD11 = 2 		: 'FCNC Down matrix element',
%	   rD12 = 3 		: 'FCNC Down matrix element',
% 	   rD13 = 5     	: 'FCNC Down matrix element',
%           rD21 = 6 		: 'FCNC Down matrix element',
%           rD22 = 1.6		: 'FCNC Down matrix element',
%	   rD23 = 0.7 		: 'FCNC Down matrix element',
%	   rD31 = 10 		: 'FCNC Down matrix element',
%	   rD32 = 11.4		: 'FCNC Down matrix element',
%	   rD33 = 11   		: 'FCNC Down matrix element'.

%let RoD = { {rD11,rD12,rD13}, {rD21,rD22,rD23}, {rD31,rD32,rD33} } .

%parameter  rU11 = 2.8 		: 'FCNC Up matrix element',
%	   rU12 = 3.3 		: 'FCNC Up matrix element',
%	   rU13 = 5.5     	: 'FCNC Up matrix element',
%          rU21 = 6.5		: 'FCNC Up matrix element',
%           rU22 = 1.2		: 'FCNC Up matrix element',
%	   rU23 = 0.9 		: 'FCNC Up matrix element',
%	   rU31 = 10.2 		: 'FCNC Up matrix element',
%	   rU32 = 1.1		: 'FCNC Up matrix element',
%	   rU33 = 7   		: 'FCNC Up matrix element'.

%let RoU = { {rU11,rU12,rU13} , {rU21,rU22,rU23}, {rU31,rU32,rU33} }. 

%%%%%%%%%%%%%%%%%%%% scalar potential parameters %%%%%%%%%%%%%%%%%%%%%%%%%

let la1 = (cbma**2*MHH**2+sbma**2*Mh**2)/vvev**2.  

let la3 = (2*MHc**2 - 2*md2)/vvev**2.

let la4 = (MA0**2 - 2*MHc**2 + sbma**2*MHH**2 + cbma**2*Mh**2)/vvev**2.

let la5 = (sbma**2*MHH**2 - MA0**2 + cbma**2*Mh**2)/vvev**2.

let la6 = -(sbma*cbma*(MHH**2 - Mh**2))/(vvev**2).

let m11 = (-cbma**2*MHH**2 - sbma**2*Mh**2)/2.

let m12 = -(sbma*cbma*(MHH**2 - Mh**2))/2.

%%%%%%%%%%%%%%%%%%%%%%%%%%% doublets scalars definition %%%%%%%%%%%%%%%%%%

let ph1 = { -i*'W+.f', (vev(vvev)+ cbma*H + sbma*h + i*'Z.f')/Sqrt2 },
    Ph1 = { i*'W-.f', (vev(vvev) + cbma*H + sbma*h - i*'Z.f')/Sqrt2 }.

let ph2 = { -i*'H+', (-sbma*H + cbma*h + i*A0)/Sqrt2 },
    Ph2 = { i*'H-', (-sbma*H + cbma*h - i*A0)/Sqrt2 }.

%%%%%%%%%%%%%%%%%%%%%%%%%%%% scalar potential %%%%%%%%%%%%%%%%%%%%%%%%%%

lterm -m11*Ph1*ph1 - md2*Ph2*ph2 + m12*(Ph1*ph2+ph1*Ph2).

lterm -la1*(Ph1*ph1)**2/2.

lterm -la2*(Ph2*ph2)**2/2.

lterm -la3*(Ph1*ph1*Ph2*ph2).

lterm -la4*(Ph1*ph2*Ph2*ph1).

lterm -la5*(Ph1*ph2*Ph1*ph2 + Ph2*ph1*Ph2*ph1)/2.

lterm -la6*(Ph1*ph1*Ph1*ph2 + ph1*Ph1*ph1*Ph2).

lterm -la7*(Ph2*ph2*Ph1*ph2 + ph2*Ph2*ph1*Ph2).

%%%%%%%%%%%%%%%%%%%%%%% kinetic terms %%%%%%%%%%%%%%%%%%%%%%%%%%%%%

let Dph1^mu^a = (deriv^mu+i*g1/2*B1^mu)*ph1^a +
	 i*g/2*taupm^a^b^c*WW^mu^c*ph1^b.

let DPh1^mu^a = (deriv^mu-i*g1/2*B1^mu)*Ph1^a 
	-i*g/2*taupm^a^b^c*{'W-'^mu,W3^mu,'W+'^mu}^c*Ph1^b.

lterm DPh1*Dph1. 

let Dph2^mu^a = (deriv^mu+i*g1/2*B1^mu)*ph2^a +
	 i*g/2*taupm^a^b^c*WW^mu^c*ph2^b.

let DPh2^mu^a = (deriv^mu-i*g1/2*B1^mu)*Ph2^a 
	-i*g/2*taupm^a^b^c*{'W-'^mu,W3^mu,'W+'^mu}^c*Ph2^b.

lterm DPh2*Dph2. 


%%%%%%%%%%%%%%%%%%%%%% yukawa coupling %%%%%%%%%%%%%%%%%%%%%%%%%%%

lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*pr*ph1 + AddHermConj )
    where
	M=Vud*Md,  pl=q1a, pr=d;           
	M=Vus*Ms, pl=q1a, pr=s;
	M=Vub*Mb, pl=q1a, pr=b;
	M=Vcd*Md,  pl=q2a, pr=d;
	M=Vcs*Ms, pl=q2a, pr=s;
	M=Vcb*Mb, pl=q2a, pr=b;
	M=Vtd*Md,  pl=q3a, pr=d;
	M=Vts*Ms, pl=q3a, pr=s;
	M=Vtb*Mb, pl=q3a, pr=b.


lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*i*tau2*pr*Ph1 
		+ AddHermConj) 
 where
	M=Mup, pl=q1a, pr=u;
	M=Mc,  pl=q2a, pr=c;
	M=Mtop,pl=q3a, pr=t.

lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*pr*ph1 + AddHermConj)
    where
	M=Me,  pl=l1,  pr=e1;
	M=Mm,  pl=l2,  pr=e2;
	M=Mt,  pl=l3,  pr=e3.

%%%%%%%%%%%%%%%%%%%%%%%% aligned-hipotesis %%%%%%%%%%%%%%%%%%%%%%%%%%%

parameter cotu = 0.9: 'cotangente betaup',
          tand = 2.5: 'tangente betadown',
          tanl = 0.8: 'tangente betalepton'. 

%let l13={n1,-tanl*e1}, L13={N1,-tanl*E1}.
%let l23={n2,-tanl*e2}, L23={N2,-tanl*E2}.
%let l33={n3,-tanl*e3}, L33={N3,-tanl*E3}. 

%let q1a3={cotu*u,-tand*(Vud*d+Vus*s+Vub*b)}, Q1a3={cotu*U,-tand*(Vud*D+Vus*S+Vub*B)}.
%let q2a3={cotu*c,-tand*(Vcd*d+Vcs*s+Vcb*b)}, Q2a3={cotu*C,-tand*(Vcd*D+Vcs*S+Vcb*B)}. 
%let q3a3={cotu*t,-tand*(Vtd*d+Vts*s+Vtb*b)}, Q3a3={cotu*T,-tand*(Vtd*D+Vts*S+Vtb*B)}.

lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*pr*ph2 + AddHermConj )
    where
	M=-tand*Vud*Md,  pl=q1a, pr=d;          
	M=-tand*Vus*Ms, pl=q1a, pr=s;
	M=-tand*Vub*Mb, pl=q1a, pr=b;
	M=-tand*Vcd*Md,  pl=q2a, pr=d;
	M=-tand*Vcs*Ms, pl=q2a, pr=s;
	M=-tand*Vcb*Mb, pl=q2a, pr=b;
	M=-tand*Vtd*Md,  pl=q3a, pr=d;
	M=-tand*Vts*Ms, pl=q3a, pr=s;
	M=-tand*Vtb*Mb, pl=q3a, pr=b.


lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*i*tau2*pr*Ph2 
		+ AddHermConj) 
 where
	M=cotu*Mup, pl=q1a, pr=u;
	M=cotu*Mc,  pl=q2a, pr=c;
	M=cotu*Mtop,pl=q3a, pr=t.
	
lterm  -(M*Sqrt2/vvev)*(anti(pl)*(1+g5)/2*pr*ph2 + AddHermConj )
    where
	M=-tanl*Me,  pl=l1,  pr=e1;
	M=-tanl*Mm,  pl=l2,  pr=e2;
	M=-tanl*Mt,  pl=l3,  pr=e3.


%%%%%%%%%%%%%%%%%%%%%%%%% ghosts %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

lterm -i*GG*f_SU3*ccghost(G)*G^mu*deriv^mu*ghost(G).
lterm  -1/2*(deriv*G)**2.

%lterm -g*eps*gh*WW1*deriv*Gh.

lterm g*eps*deriv*Gh*gh*WW1.


lterm  -1/2*(deriv*A)**2.


lterm  -1/2*(2*(deriv*'W+'+MW*'W+.f')*(deriv*'W-'+MW*'W-.f') +
	(deriv*Z+MW/CW*'Z.f')**2).


lterm -MW*EE/2/SW*((H+i*'Z.f')*('W-.C'*'W+.c' + 'W+.C'*'W-.c')
    		+H*'Z.C'*'Z.c'/CW**2-2*i*'Z.f'*'W+.C'*'W-.c').

lterm i*EE*MW/2/CW/SW*(
	'W+.f'*('W-.C'*'Z.c'*(1-2*SW**2)+'W-.c'*'Z.C'
			+2*CW*SW*'W-.C'*'A.c') -
	'W-.f'*('W+.C'*'Z.c'*(1-2*SW**2)+'W+.c'*'Z.C'
			+2*CW*SW*'W+.C'*'A.c')).

prtcprop pdg:(d=1, u=2, s=3,c=4, b=5, t=6, G=21, A=22, Z=23, 'W+'=24, h=34, e1=11, n1=12, e2=13, n2=14, e3=15, n3=16, 'A0'=35, H=36, 'H+'=37).

SetAngle(1-SW**2=CW**2).
SetAngle(sbma**2+cbma**2=1).
SetEM(A,EE).
option chepLPWidth=1484.
option SmartAngleComb=1.
CheckHerm.